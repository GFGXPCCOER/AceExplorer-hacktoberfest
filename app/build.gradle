/*
 * Copyright (C) 2017 Ace Explorer owned by Siju Sakaria
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'

android {
    signingConfigs {
        config {
            keyAlias keystoreProp['keyAlias']
            keyPassword keystoreProp['keyPassword']
            storeFile file(keystoreProp['storeFile'])
            storePassword keystoreProp['storePassword']
        }
    }
    compileSdkVersion rootProject.compileSdkVersion
    defaultConfig {
        applicationId "com.siju.acexplorer"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode versionCodeValue
        versionName versionValue
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'true'
    }

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "8g"
    }
    configurations {
        all*.exclude group: 'commons-logging', module: 'commons-logging'
    }
    packagingOptions {
        exclude 'proguard-project.txt'
        exclude 'project.properties'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/DEPENDENCIES'
        return true
    }
    buildTypes {
        debug {
            minifyEnabled false
            buildConfigField 'boolean', 'ENABLE_CRASHLYTICS', 'false'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Commented since Beta Crashlytics looks for name "app-release.apk" to auto upload build
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Ace_" + defaultConfig.versionName + ".apk"
                }
            }
            signingConfig signingConfigs.config
            return true

        }
    }
    sourceSets {
        main {
            res.srcDirs = [rootProject.resDirs]

        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }
    flavorDimensions "dimen"
    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            buildConfigField 'boolean', 'IS_DEV_VERSION', 'true'
        }
        prod {
            buildConfigField 'boolean', 'IS_DEV_VERSION', 'false'
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation fileTree(include: ['*.aar'], dir: 'libs')
    implementation project(':RootShell')

    implementation supportDependencies.appCompat
    implementation supportDependencies.recyclerView
    implementation supportDependencies.design
    implementation supportDependencies.cardView
    implementation supportDependencies.palette

    implementation firebase.core
    implementation firebase.ads
    implementation firebase.invites

    implementation google.billing
    implementation google.gson

    implementation others.kotlin
    implementation others.glide
    annotationProcessor others.glideCompiler
    implementation others.apache_compress
    implementation others.floatingButton
    implementation others.ratethisapp

    debugImplementation others.leakCanary
    releaseImplementation others.leakCanaryRel

    implementation(others.crashlytics) {
        transitive = true
    }
    testImplementation testing.junit
    androidTestImplementation(testing.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
apply plugin: 'com.google.gms.google-services'
